apply plugin: 'com.android.library'
apply plugin: 'grill'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco'

//Known issue in jacoco: https://github.com/robolectric/robolectric/issues/2230
jacoco {
    toolVersion = "${rootProject.ext.jacocoVersion}"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode grill.CI.getCommitRevision()
        versionName "${rootProject.ext.haloVersion}"
    }

    lintOptions {
        disable 'InvalidPackage' //For okio https://github.com/square/okio/issues/58
    }

    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
            jacoco {
                includeNoLocationClasses = true
            }
            setIgnoreFailures(rootProject.ext.ignoreTestFailures)
        }
    }
}

dependencies {
    compile "com.squareup.okhttp3:okhttp:${rootProject.ext.okHttpVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.appcompatVersion}"
    testCompile project(":halo-testing")
}

/**
 * Distribution
 */
group = rootProject.ext.haloGroup
version = rootProject.ext.haloVersion
description = 'The HALO framework allows the declaration in a common way of an API so it can be used properly and easily'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: "documentationRelease") {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

grill {
    distribute {
        libName 'HALO Framework'
        libDescription 'Android framework to define configurable APIs and make proper offline calls.'
        libDevId "halo-mobgen"
        libDevName "MOBGEN"
        libDevEmail "halo@mobgen.com"
        libSiteUrl "https://halo.mobgen.com"
        libGitUrl "https://github.com/mobgen/halo-android/tree/master/sdk/halo-framework"
        bintrayRepoName "HALO"
        bintrayUser "${rootProject.ext.jcenterUser}"
        bintrayKey "${rootProject.ext.jcenterKey}"
        bintrayMavenCentralUser "${rootProject.ext.sonatypeUser}"
        bintrayMavenCentralPassword "${rootProject.ext.sonatypePassword}"
        bintrayArtifactJavadoc project.tasks.getByName("javadocJar")
        bintrayArtifactSources project.tasks.getByName("sourcesJar")
        bintrayMavenSync project.hasProperty("sync")
    }
    documentation {
        templateDir "/../docs/template"
    }
    coverage {}
    codeQuality {
        host "${rootProject.ext.sonarHost}"
        projectKey "halo:framework"
        projectName "Halo Framework"
        projectVersion "${rootProject.ext.sonarVersion}"
        dbHost "${rootProject.ext.sonarDbHost}"
        dbUser "${rootProject.ext.sonarUser}"
        dbPassword "${rootProject.ext.sonarPassword}"
    }
}