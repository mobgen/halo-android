apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'grill'
apply plugin: 'jacoco'
//Knwn issue in jacoco: https://github.com/robolectric/robolectric/issues/2230
jacoco {
    toolVersion = "${rootProject.ext.jacocoVersion}"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode grill.CI.getCommitRevision()
        versionName "${rootProject.ext.haloVersion}"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
            jacoco {
                includeNoLocationClasses = true
            }
            setIgnoreFailures(rootProject.ext.ignoreTestFailures)
        }
    }

    lintOptions {
        disable 'InvalidPackage' //For okio https://github.com/square/okio/issues/58
    }
}

dependencies {
    provided project(':halo-content-annotations')
    apt project(':halo-content-processor')
    compile "com.mobgen.halo.android:halo-sdk:${rootProject.ext.haloVersion}"
    apt "com.bluelinelabs:logansquare-compiler:${rootProject.ext.loganSquareVersion}"

    testApt "com.bluelinelabs:logansquare-compiler:${rootProject.ext.loganSquareVersion}"
    testApt project(':halo-content-processor')
    testCompile "com.mobgen.halo.android:halo-testing:${rootProject.ext.haloVersion}"
}

/**
 * Distribution
 */
group = "${rootProject.ext.haloGroup}"
version = "${rootProject.ext.haloVersion}"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: "documentationRelease") {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

grill {
    distribute {
        libName 'HALO Content'
        libDescription 'Content management library to handle the halo data'
        libDevId "halo-mobgen"
        libDevName "MOBGEN"
        libDevEmail "halo@mobgen.com"
        libSiteUrl "https://halo.mobgen.com"
        libGitUrl "https://github.com/mobgen/halo-android/tree/master/sdk-libs/halo-content"
        bintrayRepoName "HALO-Content"
        bintrayUser "${rootProject.ext.jcenterUser}"
        bintrayKey "${rootProject.ext.jcenterKey}"
        bintrayMavenCentralUser "${rootProject.ext.sonatypeUser}"
        bintrayMavenCentralPassword "${rootProject.ext.sonatypePassword}"
        bintrayArtifactJavadoc project.tasks.getByName("javadocJar")
        bintrayArtifactSources project.tasks.getByName("sourcesJar")
        bintrayMavenSync project.hasProperty("sync")
    }
    documentation {
        templateDir "/../docs/template"
    }
    coverage {
        excludes(['**/*$JsonObjectMapper*.*'])
    }
    codeQuality {
        host "${rootProject.ext.sonarHost}"
        projectKey "halo:content"
        projectName "Halo Content SDK"
        projectVersion "${rootProject.ext.sonarVersion}"
        dbHost "${rootProject.ext.sonarDbHost}"
        dbUser "${rootProject.ext.sonarUser}"
        dbPassword "${rootProject.ext.sonarPassword}"
    }
}